apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"

dependencies {
    compile "com.vaadin:vaadin-server:$vaadin8_version"
    compile(project(":mock-servlet-environment"))

    testCompile("com.github.mvysny.dynatest:dynatest-engine:${dynatest_version}")
    compile "org.jetbrains.kotlin:kotlin-test"
    testCompile('org.slf4j:slf4j-simple:1.7.25')
    testCompile("com.github.vok.karibudsl:karibu-dsl-v8:0.4.11")

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'karibu-testing-v8'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Support for browserless Vaadin testing using the Kotlin language')
                root.appendNode('name', 'Karibu Testing v8')
                root.appendNode('url', 'https://github.com/mvysny/karibu-testing')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'github'
        name = 'com.github.kaributesting'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/karibu-testing'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}
