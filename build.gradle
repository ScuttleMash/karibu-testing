buildscript {
    ext.kotlin_version = '1.2.21'
    ext.vaadin8_version = '8.3.0'
    ext.vaadin10_version = '10.0.0.alpha21'
    ext.dynatest_version = "0.6"

    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
    id "com.jfrog.bintray" version "1.7.3"
}

repositories {
    mavenCentral()
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.kaributesting'
    version '0.4.1-SNAPSHOT'
}

subprojects {

    apply plugin: 'kotlin'

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        maven { url "https://dl.bintray.com/mvysny/github" }
    }

    test {
        useJUnitPlatform()
    }
}

