buildscript {
    ext.kotlin_version = '1.2.51'
    ext.vaadin8_version = '8.5.0.beta2'
    ext.vaadin10_version = '10.0.2'
    ext.dynatest_version = "0.8"

    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id "com.jfrog.bintray" version "1.8.1"
}

repositories {
    mavenCentral()
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.kaributesting'
    version '0.4.16-SNAPSHOT'
}

subprojects {

    apply plugin: 'kotlin'

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        maven { url "https://dl.bintray.com/mvysny/github" }
    }

    test {
        useJUnitPlatform()
        testLogging {
            // to see the exceptions of failed tests in Travis-CI console.
            exceptionFormat 'full'
        }
    }
}

