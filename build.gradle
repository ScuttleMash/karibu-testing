buildscript {
    ext.vaadin8_version = '8.5.2'
    ext.vaadin10_version = '11.0.1'
    ext.dynatest_version = "0.10"

    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.71"
    id "com.jfrog.bintray" version "1.8.1"
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.kaributesting'
    version '0.4.18-SNAPSHOT'
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/mvysny/github" }
    }
}

subprojects {

    apply plugin: 'kotlin'

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            // to see the exceptions of failed tests in Travis-CI console.
            exceptionFormat 'full'
        }
    }
}

